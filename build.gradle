buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.4.+'
    }
}

task clean(type: Delete) {
  subprojects.each { dependsOn("${it.name}:clean") };
  delete 'build'
}

subprojects {
    apply plugin: 'liberty'
    apply plugin: 'war'

    sourceCompatibility = 1.8

    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.4.+'
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        providedCompile group: 'org.eclipse.microprofile', name: 'microprofile', version: '1.3'
        providedCompile group: 'javax', name: 'javaee-api', version: '8.0'
        testCompile group: 'junit', name: 'junit', version: '4.+'
        testCompile group: 'org.eclipse', name: 'yasson', version: '1.0.1'
        testCompile group: 'org.glassfish', name: 'javax.json', version: '1.1.+'
        libertyRuntime group: 'io.openliberty', name: 'openliberty-runtime', version: '[18.0.0.3,)'
    }
    
    eclipse {
	    classpath {
	        defaultOutputDir =  file('build/classes/java/main')
	        file {
	            whenMerged {
	                entries.findAll { it.path.startsWith('src/test') }
	                       .each { it.output = "build/classes/java/test" }
	            }
	        }
	    }
	  }
    
    liberty {
        install {
            // use 1 liberty install for the whole repo
            baseDir = rootProject.buildDir
        }
    }

    clean.dependsOn 'libertyStop'
    libertyDebug.dependsOn 'libertyStop'    
    libertyStart.dependsOn 'libertyStop', 'test'
    libertyRun.dependsOn   'libertyStop'
    
    task debug { dependsOn 'libertyDebug' }
    task start { dependsOn 'libertyStart' }
    task stop  { dependsOn 'libertyStop'  }
    
    libertyStart.doLast {
        println "Application available at: ${appUrl}"
    }
    
    task open {
        doLast {
            java.awt.Desktop.desktop.browse "${appUrl}".toURI()
        }
    }
}
